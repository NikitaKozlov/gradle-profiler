plugins {
    id 'java'
    id 'groovy'
    id 'application'
}

repositories {
    jcenter()
    maven { url 'https://repo.gradle.org/gradle/repo' }
    maven { url "https://jitpack.io" }
}

configurations {
    profilerPlugins
    flamegraph
    compileOnly {
        extendsFrom flamegraph
    }
}

dependencies {
    compile 'org.gradle:gradle-tooling-api:4.6'
    compile 'com.google.guava:guava:21.0'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.2'
    compile 'com.typesafe:config:1.3.1'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.github.javaparser:javaparser-core:3.1.3'
    compile 'org.apache.ant:ant-compress:1.5'
    compile 'commons-io:commons-io:2.6'
    compile 'org.codehaus.groovy:groovy:2.4.7'

    profilerPlugins project(':chrome-trace')
    flamegraph("com.github.oehme:jfr-flame-graph:v0.0.10:all")

    runtime 'org.slf4j:slf4j-simple:1.7.10'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

sourceCompatibility = 1.8
mainClassName = "org.gradle.profiler.Main"

jar {
    inputs.files(configurations.flamegraph)
    from { configurations.flamegraph.collect { zipTree(it) } }
}

processResources {
    into('META-INF/jars') {
        from configurations.profilerPlugins
    }
}
